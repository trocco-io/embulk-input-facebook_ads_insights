plugins {
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "0.13.0"
    id "com.diffplug.spotless" version "5.15.0"
    id "java"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

configurations {
    provided
}

group = "io.trocco"
description = "Facebook Ads Insights input plugin for Embulk."

version = {
     def vd = versionDetails()
     if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
         vd.lastTag.substring(1)
     } else {
         "0.0.0.${vd.gitHash}.pre"
     }
 }()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.42"
    compileOnly("org.embulk:embulk-api:${embulkVersion}")
    compileOnly("org.embulk:embulk-spi:${embulkVersion}")

    compile "org.embulk:embulk-util-config:0.3.4"
    compile "org.embulk:embulk-util-timestamp:0.3.0"
    compile "org.embulk:embulk-util-json:0.3.0"

    compile group: "com.facebook.business.sdk", name: "facebook-java-business-sdk", version: "19.0.3"
    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testImplementation "junit:junit:4.+"
    testImplementation "org.mockito:mockito-core:3.11.2"
    testImplementation "org.embulk:embulk-api:${embulkVersion}"
    testImplementation "org.embulk:embulk-spi:${embulkVersion}"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
}

embulkPlugin {
    mainClass = "org.embulk.input.facebook_ads_insights.FacebookAdsInsightsInputPlugin"
    category = "input"
    type = "facebook_ads_insights"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

clean { delete "classpath" }

gem {
    from("LICENSE.txt")
    authors = ["naotaka nakane"]
    email = ["n.nakane0219@gmail.com"]
    summary = "Facebook Ads Insights input plugin for Embulk."
    homepage = "https://github.com/trocco-io/embulk-input-facebook_ads_insights"
    licenses = [ "MIT" ]
}

sourceSets.all {
    configurations.getByName(runtimeClasspathConfigurationName) {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
    configurations.getByName(compileClasspathConfigurationName) {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
    configurations.getByName(runtimeConfigurationName) {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        // googleJavaFormat()
        toggleOffOn()
    }
}
